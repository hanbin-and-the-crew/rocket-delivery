plugins {
    id 'jacoco'
}

dependencies {
    implementation project(':module-common')
    implementation project(':module-jpa')
    implementation project(':module-kafka')

    // spring cloud
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    // retry (낙관적 락 재시도)
    implementation 'org.springframework.retry:spring-retry'
    implementation 'org.springframework:spring-aspects'

    // test
    testImplementation project(':module-common')
    testImplementation project(':module-jpa')
    testImplementation 'com.h2database:h2'
    testImplementation 'io.rest-assured:rest-assured:5.3.2'
    testImplementation 'org.springframework.kafka:spring-kafka-test'

    // testcontainers
    testImplementation 'org.testcontainers:testcontainers:1.19.3'
    testImplementation 'org.testcontainers:postgresql:1.19.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.3'
    testRuntimeOnly 'org.postgresql:postgresql'

    // API 문서화 (Swagger/OpenAPI) 나중에 빼셔도 되는데 API 테스트가 안되서 잠시
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

// Jacoco
jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/config/**',
                    '**/dto/**',
                    '**/Application.class'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.70  // 70% 커버리지 요구
            }
        }
    }
}

test {
    finalizedBy jacocoTestReport
}